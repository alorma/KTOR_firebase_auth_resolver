buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.+'
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.4'
    }
}

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'kotlin'

group 'com.alorma.ktor'
version '0.0.5'

sourceSets {
    main.kotlin.srcDirs = main.java.srcDirs = ['src']
    test.kotlin.srcDirs = test.java.srcDirs = ['test']
    main.resources.srcDirs = ['resources']
    test.resources.srcDirs = ['testresources']
}

repositories {
    jcenter()
    maven { url "https://kotlin.bintray.com/ktor" }

}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile "io.ktor:ktor-server-netty:$ktor_version"
    compile "io.ktor:ktor-auth:$ktor_version"
    compile "ch.qos.logback:logback-classic:$logback_version"

    compile "io.ktor:ktor-client-apache:$ktor_version"

    testCompile "io.ktor:ktor-server-tests:$ktor_version"
}

shadowJar {
    baseName = 'firebase_auth_resolver'
    classifier = null
    version = null
}

jar {
    manifest {
        attributes(
                "Class-Path": configurations.compile.collect { it.getName() }.join(' '),
        )
    }
}

publishing {
    publications {
        LibPublication(MavenPublication) {
            from components.java
            groupId 'com.alorma.ktor'
            artifactId 'firebase_resolver'
            version '0.0.5'
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = 'Ktor-Firebase-Auth-Resolver'
        userOrg = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/alorma/KTOR_firebase_auth.git'
        labels = ['ktor', 'firebase', 'auth']
        attributes = ['gradle-plugin': 'com.use.less:com.use.less.gradle:gradle-useless-plugin']
    }
}

install {
    repositories.mavenInstaller {
        pom.project {
            name 'Ktor-Firebase-Auth-Resolver'
            description 'KTOR Firebase authentication plugin'
            url 'https://github.com/alorma/KTOR_firebase_auth_resolver'
            inceptionYear '2019'

            packaging 'jar'
            groupId 'com.alorma.ktor'
            artifactId 'firebase_auth_resolver'
            version '0.0.5'

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            scm {
                connection 'https://github.com/alorma/KTOR_firebase_auth_resolver.git'
                url 'https://github.com/alorma/KTOR_firebase_auth_resolver'

            }
            developers {
                developer {
                    name 'Alorma'
                }
            }
        }
    }
}
